apply plugin: 'maven-publish'
apply plugin: 'signing'

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        archiveClassifier.set 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }

    apply plugin: "org.jetbrains.dokka"
    tasks.dokkaJavadoc.configure {
        outputDirectory.set(file("$buildDir/javadoc"))
    }

    task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
        archiveClassifier.set 'javadoc'
        from "$buildDir/javadoc"
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set 'sources'
        from sourceSets.main.allSource
    }

    apply plugin: "org.jetbrains.dokka"
    tasks.dokkaJavadoc.configure {
        outputDirectory.set(file("$buildDir/javadoc"))
    }

    task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
        archiveClassifier.set 'javadoc'
        from "$buildDir/javadoc"
    }
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = PUBLISH_NAME
                    description = PUBLISH_DESCRIPTION
                    url = 'https://github.com/Miha-x64/Lychee'
                    licenses {
                        license {
                            name = 'Apache-2.0'
                            url = 'https://github.com/Miha-x64/Lychee/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'Miha-x64'
                            name = 'Mike'
                            email = 'silizium@ok.de'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/Miha-x64/Lychee.git'
                        developerConnection = 'scm:git:ssh://github.com/Miha-x64/Lychee.git'
                        url = 'https://github.com/Miha-x64/Lychee/tree/master'
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}
