apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion target_android_version

    defaultConfig {
        applicationId "net.aquadc.propertiesSampleApp"
        targetSdkVersion target_android_version
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("${System.getProperty('user.home')}/.android/debug.keystore")
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'v'
    productFlavors {
        legacy {
            dimension 'v'
            minSdkVersion 14
        }
        fresh {
            dimension 'v'
            minSdkVersion 21
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    packagingOptions {
        exclude 'META-INF/*kotlin*'
        exclude 'META-INF/*.version'
        exclude 'kotlin/**'
    }
}

repositories {
    maven { url "https://jitpack.io" } // com.github.grishka
}
dependencies {
    testImplementation 'junit:junit:4.12'

    freshImplementation 'com.github.grishka.litex:recyclerview:1.0'
    freshImplementation 'com.github.grishka.litex:collection:1.0'
    legacyImplementation 'androidx.recyclerview:recyclerview:1.1.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implNoX(it, "com.louiscad.splitties:splitties-views-dsl:$splitties_version")
    implNoX(it, "com.louiscad.splitties:splitties-resources:$splitties_version")
    implNoX(it, "com.louiscad.splitties:splitties-views-dsl-recyclerview:$splitties_version")
    
    implementation project(':properties')
    implementation project(':android-bindings')
    implementation project(':sql') // required for SQLite sample
    implementation project(':persistence') // required for saving state â€” ParcelPropertiesMemento is there
    implementation project(':samples:logic-sample') // ViewModels are here

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-beta-3'
}
private static void implNoX(DependencyHandler h, String what) {
    h.legacyImplementation what
    h.freshImplementation(what) {
        exclude group: 'androidx.recyclerview'
        exclude group: 'androidx.core'
        exclude group: 'androidx.collection'
        exclude group: 'androidx.arch.core'
        exclude group: 'androidx.lifecycle'
        exclude group: 'androidx.annotation'
    }
}
